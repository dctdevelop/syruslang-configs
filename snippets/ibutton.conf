define group ibutton

add ibutton john_smith DA00001C3C9C3101

## FIELDSETS
define fieldset taip fields=IB:$ibutton.connected.id,IS:$ibutton.last.id
define fieldset json fields=$ibutton

## SIGNALS
# ibutton detected
define signal ibutton_detected $ibutton.connected

# authorized driver
define signal auth_ibutton $ibutton.connected.whitelisted == true

# unauthorized driver
define signal unauth_ibutton $ibutton.connected.whitelisted == false


## EVENTS
# ibutton detected
define event ibuttonConnected group=ibutton fieldset=json ack=seq label=ibdet code=60 trigger=ibutton_detected

# authorized driver
define event authIbutton group=ibutton fieldset=taip ack=seq label=ibmtch code=61 trigger=auth_ibutton

# unauthorized driver
define event unauthIbutton group=ibutton fieldset=taip ack=seq label=ibnomtch code=62 trigger=unauth_ibutton


## ACTIONS
# action to deactivate output2 if driver authorized and speak welcome message
define action auth_driver_deactivate_out trigger=auth_ibutton set out2 off
define action speakActionIbutton trigger=auth_ibutton speak lang=en 'Welcome {{$ibutton.authorized.last.alias}}. Syrus wishes you an excellent trip'

# action to activate output2 if driver is unauthorized
define action unauth_activate_out trigger=unauth_ibutton set out2 on
define action speakActionNoIbutton trigger=unauth_ibutton speak lang=en 'You are not an authorized driver. Please contact an administrator'